buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

group= "hunternif.mc.atlas"
archivesBaseName = "antiqueatlas"

ext.mod_version = "$version_major.$version_minor.$version_patch"
version = "$mc_version-$mod_version" // This will be in the jar file name

println "Building " + archivesBaseName + " " + version

minecraft {
    version = mc_version + '-' + forge_version
    if (file('../run').exists()) {
		runDir = "../run"
	} else {
		runDir = "run"
	}
	
	replace '@VERSION@', project.version
}

dependencies {
    testCompile "junit:junit:4.+"
}

processResources
{
	// this will ensure that this task is redone when the versions change.
    inputs.property "mod_version", project.mod_version
    inputs.property "mc_version", project.mc_version
	
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mc_version
        expand 'version':project.mod_version, 'mcversion':project.mc_version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

tasks.build.dependsOn('sourceJar', 'deobfJar')

artifacts {
	archives deobfJar 
	archives sourceJar
}
